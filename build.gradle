/**
 * Copyright 2016 Duolingo
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
    }
}

Properties localProperties = new Properties()
localProperties.load(file('local.properties').newDataInputStream())

String VERSION = '1.0.0'

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

repositories {
    jcenter()
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 24
    }
}

dependencies {
    provided 'com.android.support:support-v4:24.1.1'
}

bintray {
    user = localProperties.getProperty('bintray.user')
    key = localProperties.getProperty('bintray.key')
    publications = ['release']
    publish = false
    pkg {
        repo = 'maven'
        name = 'RtlViewPager'
        userOrg = 'duolingo'
        issueTrackerUrl = 'https://github.com/duolingo/rtl-viewpager/issues'
        vcsUrl = 'https://github.com/duolingo/rtl-viewpager.git'
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
        version {
            name = VERSION
            vcsTag = 'v' + VERSION
        }
    }
}

bintrayUpload.dependsOn assembleRelease

publishing {
    publications {
        android.libraryVariants.all { variant ->
            create(variant.name,  MavenPublication) {
                variant.outputs.each { output ->
                    artifact output.outputFile
                }
                groupId 'com.duolingo.open'
                artifactId 'rtl-viewpager'
                version VERSION
            }
        }
    }
}